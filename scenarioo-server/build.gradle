/* scenarioo-server
 * Copyright (C) 2014, scenarioo.org Development Team
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

group = 'org.scenarioo'

apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'war'
apply from: "${rootProject.projectDir}/build-java.gradle"
apply plugin: 'org.springframework.boot'
apply plugin: 'cz.habarta.typescript-generator'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath group: 'cz.habarta.typescript-generator', name: 'typescript-generator-gradle-plugin', version: '2.13.489'
    }
}
generateTypeScript {
    classes = [
            'org.scenarioo.model.configuration.Configuration',
            'org.scenarioo.rest.configuration.ApplicationStatus',
            'org.scenarioo.model.configuration.BranchAlias',
            'org.scenarioo.rest.configuration.FlatLabelConfiguration',
            'org.scenarioo.dao.version.ApplicationVersion',
            'org.scenarioo.model.docu.aggregates.branches.BuildImportSummary',
            'org.scenarioo.model.diffViewer.BuildDiffInfo',
            'org.scenarioo.model.diffViewer.ScenarioDiffInfo',
            'org.scenarioo.model.diffViewer.StepDiffInfo',
            'org.scenarioo.model.diffViewer.UseCaseDiffInfo',
            'org.scenarioo.model.docu.aggregates.objects.CustomObjectTabTree',
            'org.scenarioo.model.docu.entities.generic.ObjectDescription',
            'org.scenarioo.model.docu.aggregates.objects.ObjectIndex',
            'org.scenarioo.rest.objectRepository.StepReference',
            'org.scenarioo.model.docu.aggregates.usecases.UseCaseScenarios',
            'org.scenarioo.rest.scenario.dto.ScenarioDetails',
            'org.scenarioo.rest.search.SearchResponse',
            'org.scenarioo.rest.search.SearchEngineStatus',
            'org.scenarioo.model.docu.aggregates.usecases.UseCaseSummary',
            'org.scenarioo.model.docu.aggregates.branches.BranchBuilds',
            'org.scenarioo.rest.sketcher.issue.dto.SketchIds',
    ]

    addTypeNamePrefix = 'I'
    noFileComment = true
    outputFile = '../scenarioo-client/app/generated-types/backend-types.ts'
    outputFileType = 'implementationFile'
    outputKind = 'module'
    jsonLibrary = 'jackson2'

}

dependencies {

    implementation 'org.apache.xmlgraphics:batik-transcoder'
    implementation 'org.apache.xmlgraphics:batik-codec'
    implementation 'org.apache.xmlgraphics:xmlgraphics-commons'
    implementation 'com.thetransactioncompany:cors-filter'
    implementation 'org.elasticsearch:elasticsearch'
    implementation 'org.elasticsearch.client:transport'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

    implementation 'io.springfox:springfox-swagger2'
    implementation 'io.springfox:springfox-swagger-ui'

    //These libraries are no longer provided in Java 9+, thus we need to add them manually
    implementation 'javax.xml.bind:jaxb-api'
    implementation 'com.sun.xml.bind:jaxb-core'
    implementation 'com.sun.xml.bind:jaxb-impl'
    implementation 'javax.activation:activation'

    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}


/**
 * Replace properties in important resource files
 */
processResources {
    filesMatching(['version.properties', 'banner.txt']) {
        expand(project.properties)
    }
}

bootJar {
    from('../scenarioo-client/dist') {
        into('static')
    }
    archiveFileName = 'scenarioo-viewer-' + archiveVersion.get() + '.jar'
}

bootJar.doLast {
    copy {
        from('build/libs/')
        into('build/libs/')
        include('scenarioo-viewer-' + archiveVersion.get() + '.jar')
        rename('scenarioo-viewer-' + archiveVersion.get() + '.jar', 'scenarioo-latest.jar')
    }
}

bootWar {
    from('../scenarioo-client/dist') {
        into('WEB-INF/classes/static')
    }
    archiveFileName = 'scenarioo-viewer-' + archiveVersion.get() + '.war'
}

bootWar.doLast {
    copy {
        from('build/libs/')
        into('build/libs/')
        include('scenarioo-viewer-' + archiveVersion.get() + '.war')
        rename('scenarioo-viewer-' + archiveVersion.get() + '.war', 'scenarioo-latest.war')
    }
}

bootWar.dependsOn ":scenarioo-client:build"
war.dependsOn ":scenarioo-client:build"


tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact bootWar

            groupId 'org.scenarioo'
            artifactId 'scenarioo-viewer'
            version versionWithGitCommit

            pom {
                name = 'Scenarioo Viewer'
                packaging = 'war'
                description = 'Scenarioo Viewer for automated Documentation using UI-E2E-Tests http://scenarioo.org'
                url = 'http://www.scenarioo.org'

                scm {
                    connection = 'https://github.com/scenarioo/scenarioo.git'
                    developerConnection = 'https://github.com/scenarioo/scenarioo.git'
                    url = 'https://github.com/scenarioo/scenarioo.git'
                }

                licenses {
                    license {
                        name = 'GNU GENERAL PUBLIC LICENSE'
                        url = 'https://github.com/scenarioo/scenarioo/blob/develop/LICENSE.txt'
                    }
                }

                developers {
                    developer {
                        id = 'scenarioo'
                        name = 'Scenarioo'
                        email = 'contact@scenarioo.org'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            // SNAPSHOTS: https://oss.sonatype.org/content/repositories/snapshots/
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

if (ossrhPassword) {
    signing {
        sign publishing.publications.mavenJava
    }
}
