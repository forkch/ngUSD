/* scenarioo-server
 * Copyright (C) 2014, scenarioo.org Development Team
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

group = 'org.scenarioo'

apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'war'
apply plugin: 'eclipse-wtp'
apply from: "${rootProject.projectDir}/build-java.gradle"
apply plugin: 'org.springframework.boot'


dependencies {
    compile 'com.google.guava:guava-base'
    compile 'org.apache.xmlgraphics:batik-transcoder'
    compile 'org.apache.xmlgraphics:batik-codec'
    compile 'org.apache.xmlgraphics:xmlgraphics-commons'
    compile 'com.thetransactioncompany:cors-filter'
    compile 'org.elasticsearch:elasticsearch'
    compile 'org.elasticsearch.client:transport'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'commons-io:commons-io'
    compile 'commons-fileupload:commons-fileupload'

    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

    //These libraries are no longer provided in Java 9+, thus we need to add them manually
    compile 'javax.xml.bind:jaxb-api'
    compile 'com.sun.xml.bind:jaxb-core'
    compile 'com.sun.xml.bind:jaxb-impl'
    compile 'javax.activation:activation'

    testCompile 'org.mockito:mockito-all'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

eclipse {
  wtp {
    component {
      contextPath = 'scenarioo'
      deployName = 'scenarioo'
    }
  }
}

task printPathConfigInformationForNodeJSAndGrunt << {
	if (project.hasProperty('pathToNodeJS')) {
        println "Path to Node JS was configued as: " + pathToNodeJS
    } else {
    	println "Path to Node JS not specified, set property 'pathToNodeJS' in case the PATH variable is not set properly or not propagated to the client's build script."
    }
    if (project.hasProperty('installGrunt')) {
        println "installGrunt is specified, this will trigger installation of grunt in client build: " + installGrunt + " (this value should be 'installGrunt')"
    } else {
        println "installGrunt: property is undefined, this means grunt is assumed to be installed already."
    }
}

task createVersionPropertiesFile << {
	def aggregatedDataFormatVersion = project.scenariooAggregatedDataFormatVersion
	def apiVersion = project.scenariooApiVersion
    def documentationVersion = project.documentationVersion

	File versionFile = new File(sourceSets.main.output.classesDir.getAbsolutePath() + '/version.properties')
	versionFile.write('version=' + versionWithGitCommit + '\n' +
            'build-date=' + new Date() + '\n' +
            'apiVersion=' + apiVersion + '\n' +
            'aggregatedDataFormatVersion=' + aggregatedDataFormatVersion + '\n' +
            'documentationVersion=' + documentationVersion + '\n')
}

bootJar {
    from('../scenarioo-client/dist') {
        into('static')
    }
    archiveName 'scenarioo-viewer-' + version + '.jar'
}

bootJar.doLast {
    copy {
        from ('build/libs/')
        into ('build/libs/')
        include('scenarioo-viewer-' + version + '.jar')
        rename('scenarioo-viewer-' + version + '.jar', 'scenarioo-latest.jar')
    }
}

bootWar {
    from('../scenarioo-client/dist') {
        into('WEB-INF/classes/static')
    }
	archiveName 'scenarioo-viewer-' + version + '.war'
}

bootWar.doLast {
    copy {
        from ('build/libs/')
        into ('build/libs/')
        include('scenarioo-viewer-' + version + '.war')
        rename('scenarioo-viewer-' + version + '.war', 'scenarioo-latest.war')
    }
}

bootWar.dependsOn createVersionPropertiesFile
war.dependsOn createVersionPropertiesFile
bootWar.dependsOn ":scenarioo-client:build"
war.dependsOn ":scenarioo-client:build"

if(ossrhPassword) {
    signing {
        sign configurations.archives
    }
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Scenarioo Viewer'
                packaging 'war'
                description 'Scenarioo Viewer for automated Documentation using UI-E2E-Tests http://scenarioo.org'
                url 'http://www.scenarioo.org'

                scm {
                    connection 'https://github.com/scenarioo/scenarioo.git'
                    developerConnection 'https://github.com/scenarioo/scenarioo.git'
                    url 'https://github.com/scenarioo/scenarioo.git'
                }

                licenses {
                    license {
                        name 'GNU GENERAL PUBLIC LICENSE'
                        url 'https://github.com/scenarioo/scenarioo/blob/develop/LICENSE.txt'
                    }
                }

                developers {
                    developer {
                        id 'scenarioo'
                        name 'Scenarioo'
                        email 'contact@scenarioo.org'
                    }
                }
            }
        }
    }
}
